plugins {
    id 'com.android.application'
}

def keystoreFilepath = ''
def keystorePSW = ''
def keystoreAlias = ''
def keystoreAliasPSW = ''
// default keystore file, PLZ config file path in local.properties
def keyfile = file('s.keystore.temp')

Properties properties = new Properties()
// local.properties file in the root director
properties.load(project.rootProject.file('local.properties').newDataInputStream())
keystoreFilepath = properties.getProperty("keystore.path")

if (keystoreFilepath) {
    keystorePSW = properties.getProperty("keystore.password")
    keystoreAlias = properties.getProperty("keystore.alias")
    keystoreAliasPSW = properties.getProperty("keystore.alias_password")
    keyfile = file(keystoreFilepath)
}

//版本号
def gitVersionCode() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-list', 'HEAD', '--first-parent', '--count'
            standardOutput = stdout
            workingDir project.rootDir
        }
        return stdout.toString().trim().toInteger()
    } catch (e) {
        logger.warn("Could not get git commit count: ${e.message}")
        return 1
    }
}

//获取short版本的 git commit id
def getGitRevision() {
    try {
        def stdout = new ByteArrayOutputStream()
        exec {
            commandLine 'git', 'rev-parse', '--short', 'HEAD'
            standardOutput = stdout
            workingDir project.rootDir
        }
        return stdout.toString().trim()
    } catch (Exception e) {
        return "unknown"
    }
}

android {
    namespace 'com.cfks.telefriends'
    compileSdk 27
    
    defaultConfig {
        applicationId "com.cfks.telefriends"
        minSdk 21   //支持Android5.0-8.1
        targetSdk 27
        versionCode gitVersionCode()
        versionName getGitRevision()
        
        vectorDrawables { 
            useSupportLibrary true
        }
    }
    
    signingConfigs {
       release {
           keyAlias keystoreAlias
           keyPassword keystoreAliasPSW
           storeFile keyfile
           storePassword keystorePSW
       }
       debug {
           keyAlias keystoreAlias
           keyPassword keystoreAliasPSW
           storeFile keyfile
           storePassword keystorePSW
       }
   }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            zipAlignEnabled true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            //signingConfig signingConfigs.release
            //签名文件存在，则签名
            if (keyfile.exists()) {
               println("WITH -> buildTypes -> release: using jks key")
               signingConfig signingConfigs.release
            } else {
               println("WITH -> buildTypes -> release: using default key")
            }
        }
        debug{
            // 为了不和 release 版本冲突
            applicationIdSuffix ".debug"
            if (keyfile.exists()) {
               println("WITH -> buildTypes -> debug: using jks key")
               signingConfig signingConfigs.debug
            } else {
               println("WITH -> buildTypes -> debug: using default key")
            }
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    buildFeatures {
        viewBinding true
    }
    
}

def supportLibraryVersion = "27.1.1"

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:support-v4:$supportLibraryVersion"
    implementation "com.android.support:cardview-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation "android.arch.lifecycle:extensions:1.1.1"
    annotationProcessor "android.arch.lifecycle:compiler:1.1.1"
    implementation 'android.arch.lifecycle:viewmodel:1.1.1'
    implementation 'android.arch.lifecycle:livedata:1.1.1'
    implementation 'de.hdodenhof:circleimageview:2.2.0'
    implementation 'cn.yc:ToolLib:1.0.0'
    implementation "io.reactivex.rxjava2:rxandroid:2.0.2"
    implementation 'com.squareup.okhttp:okhttp:2.7.5'
    implementation 'com.squareup.okio:okio:1.7.0'
    //glide
    implementation 'com.github.bumptech.glide:glide:4.8.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.8.0'
}